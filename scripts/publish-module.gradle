apply plugin: 'maven-publish'
apply plugin: 'signing'

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

//artifacts {
//    archives androidSourcesJar
//}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    afterEvaluate {
                        if (components.findByName("release") != null) {
                            from components.release
                        } else {
                            throw new GradleException("Android release component not found. Ensure your `com.android.library` plugin is applied correctly.")
                        }
                    }
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Ortto Android SDK'
                    url = 'https://github.com/autopilot3/ap3-push-android-sdk'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/autopilot3/ap3-push-android-sdk/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = "ortto"
                            name = "Ortto.com Team"
                            email = "contact@ortto.com"
                        }
                        developer {
                            id = 'mitchflindell'
                            name = 'Mitch Flindell'
                            email = 'mitch@ortto.com'
                        }
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        url = "https://github.com/autopilot3/ap3-push-android-sdk"
                        connection = "scm:git@github.com:autopilot3/ap3-push-android-sdk.git"
                        developerConnection = "scm:git@github.com:autopilot3/ap3-push-android-sdk.git"
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}