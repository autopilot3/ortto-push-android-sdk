plugins {
    id 'com.android.library'
}

def publishVersion = System.getenv("VERSION_NAME")
if (publishVersion == null || publishVersion.isEmpty()) {
    publishVersion = "0.0.0"
}

ext {
    PUBLISH_GROUP_ID = 'com.ortto'
    PUBLISH_VERSION = publishVersion.replaceFirst("^v", "")
    PUBLISH_ARTIFACT_ID = 'androidsdk'
}

android {
    namespace "com.ortto.messaging"
    compileSdkVersion 34

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }

    defaultConfig {
        minSdk 26
        targetSdk 33
        versionCode 3
        versionName PUBLISH_VERSION
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            buildConfigField "String", "SDK_VERSION", "\""+PUBLISH_VERSION+"\""
            minifyEnabled false
            consumerProguardFiles 'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField "String", "SDK_VERSION", "\""+PUBLISH_VERSION+"\""
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_18
        targetCompatibility JavaVersion.VERSION_18
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.2'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation platform('com.google.firebase:firebase-bom:31.0.2')

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    implementation 'com.google.firebase:firebase-messaging'
    implementation 'androidx.webkit:webkit:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.robolectric:robolectric:4.10.3"
    testImplementation 'org.mockito:mockito-core:5.8.0'
}

if ("Messaging SDK" == rootProject.name) {
    apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"
}

